#Maintainer: archdevlab <https://github.com/archdevlab>

pkgname=lib32-vulkan-profiles-git
pkgdesc='Vulkan Profiles Toolset (32-bit) (git version)'
pkgver=1.3.292.r0.gfb8b740
_major=1.3
pkgrel=1
arch=(i686 x86_64)
url='https://github.com/KhronosGroup/Vulkan-Profiles'
license=(Apache-2.0 BSD-2-Clause BSL-1.0)
makedepends=(cmake git ninja make valijson-git lib32-jsoncpp-cmake-git python-jsonschema vulkan-headers lib32-vulkan-icd-loader lib32-vulkan-utility-libraries)
depends=(lib32-gcc-libs lib32-vulkan-icd-loader vulkan-headers lib32-vulkan-utility-libraries lib32-libx11)
# For the layer JSON description
depends+=(vulkan-profiles)
conflicts=(lib32-vulkan-profiles)
provides=(lib32-vulkan-profiles)
source=(git+https://github.com/KhronosGroup/Vulkan-Profiles.git)

pkgver(){
  cd "${srcdir}"/Vulkan-Profiles
  git describe --long --tags --abbrev=7 --match='v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  #git describe --long --tags --abbrev=7 --exclude 'roadmap*' | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g'
  #git describe --long --tags --abbrev=7 --exclude 'roadmap*' | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g' | sed 's/.0//g'
  #echo "${_major}".$(sed -n 9p scripts/known_good.json | sed 's/[^0-9]*//g' | cut -c 3-).r$(git rev-list --count HEAD).g$(git rev-parse --short --abbrev=7 HEAD)
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export CFLAGS+=" -fPIC"
export CXXFLAGS+=" -fPIC"
export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/Vulkan-Profiles \
  -D UPDATE_DEPS=OFF \
  -G Ninja \
  -D CMAKE_C_FLAGS=-m32 \
  -D CMAKE_CXX_FLAGS=-m32 \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_BINDIR=bin \
  -D CMAKE_INSTALL_LIBDIR=lib32 \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D VULKAN_HEADERS_INSTALL_DIR=/usr \
  -D VULKAN_UTILITY_LIBRARIES_INSTALL_DIR=/usr \
  -D VULKAN_LOADER_INSTALL_DIR=/usr \
  -D JSONCPP_INSTALL_DIR=/usr \
  -D VALIJSON_INSTALL_DIR=/usr \
  -D BUILD_TESTS=OFF \
  -D ANDROID=OFF \
  -D BUILD_TESTS_EXTRA=OFF \
  -Wno-dev

  ninja -j$(nproc) -C "${srcdir}"/build
}

package(){
  DESTDIR="${pkgdir}" ninja -j$(nproc) -C "${srcdir}"/build install

  rm -rf "${pkgdir}"/usr/{include,share}

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/Vulkan-Profiles/LICENSES/* "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:

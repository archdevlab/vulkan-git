#Maintainer: archdevlab <https://github.com/archdevlab>

pkgname=vulkan-profiles-git
pkgdesc='Vulkan Profiles Toolset (git version)'
pkgver=1.3.288.r0.g1737ca6
_major=1.3
pkgrel=1
arch=(x86_64)
url='https://github.com/KhronosGroup/Vulkan-Profiles'
license=(Apache-2.0 BSD-2-Clause BSL-1.0)
makedepends=(cmake git ninja make valijson-git jsoncpp-cmake-git python-jsonschema vulkan-headers vulkan-icd-loader vulkan-utility-libraries)
depends=(gcc-libs vulkan-icd-loader vulkan-headers vulkan-utility-libraries libx11)
conflicts=(vulkan-profiles)
provides=(vulkan-profiles)
source=(git+https://github.com/KhronosGroup/Vulkan-Profiles.git)

pkgver(){
  cd "${srcdir}"/Vulkan-Profiles
  git describe --long --tags --abbrev=7 --match='v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  #git describe --long --tags --abbrev=7 --exclude 'roadmap*' | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g'
  #git describe --long --tags --abbrev=7 --exclude 'roadmap*' | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g' | sed 's/.0//g'
  #echo "${_major}".$(sed -n 9p scripts/known_good.json | sed 's/[^0-9]*//g' | cut -c 3-).r$(git rev-list --count HEAD).g$(git rev-parse --short --abbrev=7 HEAD)
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/Vulkan-Profiles \
  -D UPDATE_DEPS=OFF \
  -G "Unix Makefiles" \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_BINDIR=bin \
  -D CMAKE_INSTALL_LIBDIR=lib \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D VULKAN_HEADERS_INSTALL_DIR=/usr \
  -D VULKAN_UTILITY_LIBRARIES_INSTALL_DIR=/usr \
  -D VULKAN_LOADER_INSTALL_DIR=/usr \
  -D JSONCPP_INSTALL_DIR=/usr \
  -D VALIJSON_INSTALL_DIR=/usr \
  -D BUILD_TESTS=OFF \
  -D ANDROID=OFF \
  -D BUILD_TESTS_EXTRA=OFF \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/Vulkan-Profiles/LICENSES/* "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:

#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Alexandre Bouvier <contact@amb.tf>

pkgname=volk-git
pkgdesc='Meta loader for Vulkan API (git version)'
pkgver=1.3.289.r353.g692bef4
_major=1.3
pkgrel=1
arch=(x86_64)
url='https://github.com/zeux/volk'
license=(MIT)
depends=(vulkan-headers vulkan-icd-loader)
makedepends=(cmake git ninja make)
conflicts=(volk)
provides=(volk)
options=(staticlibs)
source=(git+https://github.com/zeux/volk.git)

pkgver(){
  cd "${srcdir}"/volk
  #git describe --long --tags --abbrev=7 | sed 's/^vulkan-sdk-//;s/\([^-]*-g\)/r\1/;s/-/./g'
  #git describe --long --tags --abbrev=7 | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g'
  #git describe --long --tags --abbrev=7 | sed -r 's,^[^0-9]+,,;s,([0-9]*-g),r\1,;s,[-_],.,g' | sed 's/.0//g'
  echo "${_major}".$(sed -n 7p CMakeLists.txt | sed 's/[^0-9]*//g').r$(git rev-list --count HEAD).g$(git rev-parse --short --abbrev=7 HEAD)
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/volk \
  -G Ninja \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_BINDIR=bin \
  -D CMAKE_INSTALL_LIBDIR=lib \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D VULKAN_HEADERS_INSTALL_DIR=/usr \
  -D Vulkan_INCLUDE_DIRS=/usr/include \
  -D VOLK_INSTALL=ON \
  -Wno-dev

  ninja -j$(nproc) -C "${srcdir}"/build
}

package(){
  DESTDIR="${pkgdir}" ninja -j$(nproc) -C "${srcdir}"/build install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/volk/LICENSE.md "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
